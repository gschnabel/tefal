subroutine talysinfo
!
!-----------------------------------------------------------------------------------------------------------------------------------
! Purpose   : Basic reaction information from TALYS
!
! Author    : Arjan Koning
!
! 2021-12-30: Original code
!-----------------------------------------------------------------------------------------------------------------------------------
!
! *** Use data from other modules
!
  use A0_tefal_mod
!
! All global variables
!   numenin         ! number of incident energies
! Constants
!   parA            ! mass number of particle
!   parZ            ! charge number of particle
! Variables for TALYS info
!   Ainit           ! mass number of initial compound nucleus
!   Atarget         ! mass number of nucleus
!   eninc           ! incident energy
!   flagblock       ! flag to block spectra, angle and gamma files
!   flagelastic     ! flag to give priority to elastic cross section for normalization
!   flagtalysdet    ! flag for detailed ENDF - 6 information from TALYS
!   flagtalysrec    ! flag for recoil information from TALYS
!   flagtalysurr    ! flag for URR information from TALYS
!   k0              ! index of incident particle
!   Lisomer         ! isomeric number of target
!   Ltarget         ! excited level of target
!   NLmax           ! maximum number of included discrete levels
!   numinc          ! number of incident energies
!   targetE         ! energy of target
!   targetspin      ! spin of target
!   tarmass         ! mass of nucleus
!   Zinit           ! charge number of initial compound nucleus
!   Ztarget         ! charge number of nucleus
!
! *** Declaration of local data
!
  implicit none
  logical           :: lexist        ! logical to determine existence
  character(len=1)  :: ch1           ! character
  character(len=1)  :: ch2           ! character
  character(len=1)  :: ch3           ! character
  character(len=1)  :: ch4           ! character
  character(len=1)  :: ch5           ! character
  character(len=9)  :: infofile      ! file with information
  character(len=132):: energyfile    ! file with incident energies
  character(len=132):: string        ! string
  integer           :: nin           ! counter for incident energy
  integer           :: istat         ! logical for file access
  integer           :: ix            !
!
! ****************** Read basic nuclear information ********************
!
! Obviously, TEFAL can only work after a TALYS run.
! Therefore, we check whether there is a so-called information file generated by TALYS (this is automatically created by
!  TALYS if 'endf y').
!
  infofile = 'tefal.inf'
  inquire (file = infofile, exist = lexist)
  if ( .not. lexist) then
    write(*, '(" TEFAL-error: Missing info file: ", a9)') infofile
    stop
  endif
  open (unit = 1, file = infofile, status = 'old')
  read(1, '(i3)') k0
  read(1, '(i3)') Ztarget
  read(1, '(i3)') Atarget
  read(1, '(f10.6)') tarmass
  read(1, '(f4.1)') targetspin
  read(1, '(f10.6)') targetE
  read(1, '(2i4)') Ltarget, Lisomer
  read(1, '(i4)') numinc
  read(1, '(i4)') NLmax
  read(1, '(a)') string
  ix = index(string,' ')
  energyfile = string(1:ix)
  read(1, '(a1)') ch1
  read(1, '(a1)') ch2
  read(1, '(a1)') ch3
  read(1, '(a1)') ch4
  do
    read(1, '(a1)', iostat = istat) ch5
    if (istat /= 0) exit
  enddo
  close (unit = 1)
  Zinit = Ztarget + parZ(k0)
  Ainit = Atarget + parA(k0)
  flagtalysdet = .false.
  flagelastic = .false.
  flagtalysrec = .false.
  flagtalysurr = .false.
  flagblock = .false.
  if (ch1 == 'y') flagtalysdet = .true.
  if (ch2 == 'y') flagelastic = .true.
  if (ch3 == 'y') flagtalysrec = .true.
  if (ch4 == 'y') flagtalysurr = .true.
  if (ch5 == 'y') flagblock = .true.
!
! ********************** Read incident energies ************************
!
! A file with incident energies (also used by TALYS) must exist.
! There is a maximum number of incident energies
!
  if (numinc > numenin) then
    write(*, '(" TEFAL-error: there are more than", i3, " incident energies in file ", a)') numenin, trim(energyfile)
    write(*, '(" numenin in tefal.cmb should be increased")')
    stop
  endif
  inquire (file = energyfile, exist = lexist)
  if ( .not. lexist) then
    write(*, '(" TEFAL-error: Missing energy file: ", a)') trim(energyfile)
    stop
  endif
  open (unit = 1, file = energyfile, status = 'old')
  eninc(0) = 0.
  do nin = 1, numinc
    read(1, * ) eninc(nin)
  enddo
  close (unit = 1)
  return
end subroutine talysinfo
! Copyright A.J. Koning 2021
